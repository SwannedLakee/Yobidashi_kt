apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply from: 'signing/config.gradle', to: android

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    byte verMajor  = 1
    byte verMiddle = 0
    byte verMinor  = 21

    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = 'tk-bot@api-6327630184664180575-715630.iam.gserviceaccount.com'
            jsonFile = file('signing/Google Play Android Developer-cbf2176b721a.json')
        }
    }

    play {
        track = 'beta'
        untrackOld = true
    }

    defaultConfig {
        applicationId "jp.toastkid.yobidashi"
        minSdkVersion 16
        targetSdkVersion 26
        if (verMiddle >= 100 || verMinor >= 10_000) {
            throw new IllegalStateException("Middle and Minor version value is allowed under 100.")
        }
        versionCode ((verMajor * 1_000_000) + (verMiddle * 10_000) + verMinor)
        versionName "${verMajor}.${verMiddle}.${verMinor}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }
    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".d"
            manifestPlaceholders = [app_name: "Xobidashi"]
            versionNameSuffix = ".d"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [app_name: "@string/app_name"]
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {

    def supportLibVersion = '26.1.0'
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:customtabs:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"

    def firebaseVersion = '11.6.2'
    implementation "com.google.firebase:firebase-core:${firebaseVersion}"
    implementation "com.google.firebase:firebase-ads:${firebaseVersion}"
    implementation "com.google.firebase:firebase-crash:${firebaseVersion}"
    implementation "com.google.android.gms:play-services-oss-licenses:${firebaseVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation 'com.github.gfx.android.orma:orma:4.2.5'

    kapt 'com.android.databinding:compiler:3.0.1'
    kapt 'com.github.gfx.android.orma:orma-processor:4.2.5'

    def leakcanaryVersion = '1.5.4'
    debugImplementation   "com.squareup.leakcanary:leakcanary-android:${leakcanaryVersion}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryVersion}"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.5"
}

repositories{
    flatDir{
        dirs 'libs'
    }
}

apply plugin: 'com.google.gms.google-services'