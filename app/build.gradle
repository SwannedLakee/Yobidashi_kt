apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'io.fabric'

apply from: 'signing/config.gradle', to: android

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    byte verMajor  = 1
    byte verMiddle = 2
    byte verMinor  = 0

    defaultConfig {
        applicationId "jp.toastkid.yobidashi"
        minSdkVersion 16
        targetSdkVersion 28
        if (verMiddle >= 100 || verMinor >= 10_000) {
            throw new IllegalStateException("Middle and Minor version value is allowed under 100.")
        }
        versionCode ((verMajor * 1_000_000) + (verMiddle * 10_000) + verMinor)
        versionName "${verMajor}.${verMiddle}.${verMinor}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".d"
            manifestPlaceholders = [app_name: "Xobidashi"]
            versionNameSuffix = ".d"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [app_name: "@string/app_name"]
        }
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

play {
    track = 'beta'
    serviceAccountEmail = 'tk-bot@api-6327630184664180575-715630.iam.gserviceaccount.com'
    serviceAccountCredentials = file('signing/api-6327630184664180575-715630-73af1e35d196.p12')
}

dependencies {

    def supportLibVersion = '1.0.0'
    implementation "androidx.appcompat:appcompat:1.1.0-rc01"
    implementation "androidx.browser:browser:${supportLibVersion}"
    implementation "androidx.cardview:cardview:${supportLibVersion}"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    def roomVersion = '2.1.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    implementation "com.google.android.material:material:1.0.0"

    implementation "com.google.firebase:firebase-core:16.0.1"
    implementation 'com.google.firebase:firebase-ml-natural-language:20.0.0'
    implementation 'com.google.firebase:firebase-ml-natural-language-translate-model:20.0.0'
    implementation 'com.google.firebase:firebase-ml-natural-language-language-id-model:20.0.0'

    implementation "com.google.android.gms:play-services-oss-licenses:16.0.1"

    implementation 'androidx.core:core-ktx:1.1.0-alpha05'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.squareup.moshi:moshi:1.7.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.2'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.6'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.2.1"
    testImplementation "io.mockk:mockk:1.9.3"
}

repositories{
    flatDir{
        dirs 'libs'
    }
}

kapt {
    javacOptions {
        option("-Xmaxerrs", 5000)
    }
}

apply plugin: 'com.google.gms.google-services'
