apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'
apply plugin: "jacoco"
apply plugin: "com.cookpad.android.plugin.license-tools"

apply from: 'signing/config.gradle', to: android

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId "jp.toastkid.yobidashi"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk

        versionCode versionCodeValue
        versionName versionNameValue
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
            applicationIdSuffix ".d"
            manifestPlaceholders = [app_name: "Xobidashi"]
            versionNameSuffix = ".d"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [app_name: "@string/app_name"]
        }
    }
    buildFeatures {
        dataBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

play {
    serviceAccountCredentials.set(file("signing/Google Play Android Developer-cbf2176b721a.json"))
    track = 'alpha'
}

dependencies {
    implementation project(":lib")
    implementation project(":article")
    implementation project(":search")
    implementation project(":todo")
    implementation project(":licenses")

    implementation "androidx.appcompat:appcompat:$libVersions.appcompat"
    implementation "androidx.cardview:cardview:$libVersions.cardview"
    implementation "androidx.constraintlayout:constraintlayout:$libVersions.constraintlayout"
    implementation "androidx.exifinterface:exifinterface:1.3.2"
    implementation "androidx.work:work-runtime:2.3.0"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation "androidx.webkit:webkit:1.4.0"

    implementation "androidx.room:room-runtime:$libVersions.room"
    kapt "androidx.room:room-compiler:$libVersions.room"

    implementation "com.google.android.material:material:$libVersions.material"

    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation "androidx.core:core-ktx:$libVersions.ktx"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$libVersions.coroutines"
    implementation "com.jakewharton.timber:timber:$libVersions.timber"
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.2.0'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.2'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation "io.coil-kt:coil:1.2.1"
    implementation "io.coil-kt:coil-gif:1.2.1"

    testImplementation "junit:junit:$libVersions.junit"
    testImplementation "org.robolectric:robolectric:$libVersions.robolectric"
    testImplementation "io.mockk:mockk:$libVersions.mockk"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$libVersions.coroutines_test"
}

repositories{
    flatDir{
        dirs 'libs'
    }
}

kapt {
    javacOptions {
        option("-Xmaxerrs", 5000)
    }
}

jacoco {
    toolVersion = "0.8.5"
}

task jacocoTestReport(
        type: JacocoReport,
        dependsOn: "testDebugUnitTest",
        group: "verification"
) {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    getSourceDirectories().from = "${projectDir}/src/main/java"
    getClassDirectories().from = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug")
    getExecutionData().from = "${buildDir}/jacoco/testDebugUnitTest.exec"
}
