apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    byte verMajor  = 1;
    byte verMiddle = 0;
    byte verMinor  = 3;

    defaultConfig {
        applicationId "jp.toastkid.yobidashi"
        minSdkVersion 16
        targetSdkVersion 26
        if (verMiddle >= 100 || verMinor >= 100) {
            throw new IllegalStateException("Middle and Minor version value is allowed under 100.")
        }
        versionCode ((verMajor * 10000) + (verMiddle * 100) + verMinor)
        versionName "${verMajor}.${verMiddle}.${verMinor}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {

    def supportLibVersion = '26.0.1'
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:customtabs:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"

    def firebaseVersion = '10.2.1'
    compile "com.google.firebase:firebase-core:${firebaseVersion}"
    compile "com.google.firebase:firebase-ads:${firebaseVersion}"
    compile "com.google.firebase:firebase-crash:${firebaseVersion}"

    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.larswerkman:HoloColorPicker:1.5'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.4'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.journeyapps:zxing-android-embedded:3.5.0'
    compile 'com.squareup.moshi:moshi:1.5.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.github.gfx.android.orma:orma:4.2.5'

    kapt 'com.android.databinding:compiler:2.3.3'
    kapt 'com.github.gfx.android.orma:orma-processor:4.2.5'

    def leakcanaryVersion = '1.5.2'
    debugCompile   "com.squareup.leakcanary:leakcanary-android:${leakcanaryVersion}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryVersion}"

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.4.2"
}

repositories{
    flatDir{
        dirs 'libs'
    }
}

apply plugin: 'com.google.gms.google-services'