/*
 * Copyright (c) 2021 toastkidjp.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompany this distribution.
 * The Eclipse Public License is available at http://www.eclipse.org/legal/epl-v10.html.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    final byte verMajor  = 2
    final byte verMiddle = 0
    final byte verMinor  = 7

    if (verMiddle >= 100 || verMinor >= 10_000) {
        throw new IllegalStateException("Middle and Minor version value is allowed under 100.")
    }

    ext.versionCodeValue = ((verMajor * 1_000_000) + (verMiddle * 10_000) + verMinor)
    ext.versionNameValue = "${verMajor}.${verMiddle}.${verMinor}"

    ext.minSdk = 24
    ext.compileSdk = 31
    ext.targetSdk = 31
    ext.buildTools = "31.0.0"

    ext.kotlin_version = '1.6.10'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" } // For Play publisher plugin
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.triplet.gradle:play-publisher:3.7.0'
    }

    ext.libVersions = [
            coroutines: '1.5.1',
            ktx: '1.1.0',
            lifecycle: '2.4.0',
            room: '2.4.0',
            paging: '3.1.0',
            timber: '4.6.0',
            okhttp: '4.9.0',
            jsoup: '1.14.3',
            exifinterface: '1.3.3',
            compose: '1.1.1',
            navigation_compose: '2.4.1',
            coil_compose: '2.0.0-rc01',
            paging_compose: '1.0.0-alpha14',
            junit: '4.12',
            robolectric: '4.2.1',
            mockk: '1.10.6',
            coroutines_test: '1.5.1'
    ]
    ext.pluginVersions = [
            jacoco: '0.8.7'
    ]
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id "com.cookpad.android.plugin.license-tools" version "1.2.8"
}

apply plugin: "jacoco"

jacoco {
    toolVersion = pluginVersions.jacoco
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" } // For PhotoView
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task jacocoMergedTestReport(
    type: JacocoReport,
    group: "verification"
) {
    gradle.afterProject { project, _ ->
        if (project.rootProject != project && project.plugins.hasPlugin('jacoco')) {
            getExecutionData().from += "${project.buildDir}/jacoco/testDebugUnitTest.exec"
            getSourceDirectories().from += "${project.projectDir}/src/main/java"
            getClassDirectories().from += project.fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/debug")
        }
    }
    reports {
        xml.getRequired().set(true)
        html.getRequired().set(true)
    }
}

task mergeDetektReport(type: io.gitlab.arturbosch.detekt.report.ReportMergeTask) {
    output = project.layout.buildDirectory.file("reports/detekt/merge.xml")
}

subprojects {
    plugins.withType(io.gitlab.arturbosch.detekt.DetektPlugin) {
        tasks.withType(io.gitlab.arturbosch.detekt.Detekt) { detektTask ->
            finalizedBy(mergeDetektReport)

            mergeDetektReport.configure { mergeTask ->
                mergeTask.input.from(detektTask.xmlReportFile)
            }
        }
    }
}