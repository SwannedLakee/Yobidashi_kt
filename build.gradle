// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    final byte verMajor  = 1
    final byte verMiddle = 6
    final byte verMinor  = 14

    if (verMiddle >= 100 || verMinor >= 10_000) {
        throw new IllegalStateException("Middle and Minor version value is allowed under 100.")
    }

    ext.versionCodeValue = ((verMajor * 1_000_000) + (verMiddle * 10_000) + verMinor)
    ext.versionNameValue = "${verMajor}.${verMiddle}.${verMinor}"

    ext.minSdk = 24
    ext.compileSdk = 30
    ext.targetSdk = 30
    ext.buildTools = "30.0.0"

    ext.kotlin_version = '1.5.21'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" } // For Play publisher plugin
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.triplet.gradle:play-publisher:3.5.0'
    }

    ext.libVersions = [
            coroutines: '1.5.1',
            ktx: '1.1.0',
            appcompat: '1.3.1',
            cardview: '1.0.0',
            constraintlayout: '2.0.2',
            lifecycle: '2.2.0',
            room: '2.3.0-alpha02',
            paging: '3.0.0-alpha07',
            material: '1.4.0',
            fragment: '1.2.4',
            timber: '4.6.0',
            junit: '4.12',
            robolectric: '4.2.1',
            mockk: '1.10.6',
            coroutines_test: '1.5.1'
    ]
    ext.pluginVersions = [
            jacoco: '0.8.7'
    ]
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.16.0"
    id "com.cookpad.android.plugin.license-tools" version "1.2.8"
}

apply plugin: "jacoco"

jacoco {
    toolVersion = pluginVersions.jacoco
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" } // For PhotoView
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task mergeJacocoFiles(
    type: JacocoMerge,
    group: "verification"
) {
    gradle.afterProject { project, _ ->
        if (project.rootProject != project && project.plugins.hasPlugin('jacoco')) {
            executionData "${project.buildDir}/jacoco/testDebugUnitTest.exec"
        }
    }
}

task jacocoMergedTestReport(
    type: JacocoReport,
    dependsOn: [tasks.mergeJacocoFiles],
    group: "verification"
) {
    getExecutionData().from = mergeJacocoFiles.destinationFile

    gradle.afterProject { project, _ ->
        if (project.rootProject != project && project.plugins.hasPlugin('jacoco')) {
            getSourceDirectories().from += "${project.projectDir}/src/main/java"
            getClassDirectories().from += project.fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/debug")
        }
    }
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
